10 PRINT "ADVENT OF CODE 2025 - DAY 1, PART A"
20 PRINT "COPYRIGHT 2025 BY JONATHAN HULL"

100 FILE$ = "INPUT"
110 D$ = CHR$(4)
120 P = 50
130 Z = 0
180 DB = 0
190 ONERR GOTO 2000

200 PRINT D$;"OPEN ";FILE$
210 PRINT D$;"READ ";FILE$
220 INPUT A$
225 IF A$ = "" THEN GOTO 2000
230 IF DB = -1 THEN PRINT "LINE: ";A$
240 DIR$ = LEFT$(A$,1)
250 NUM$ = MID$(A$,2)
260 IF DB = -1 THEN PRINT "DIRECTION: ";DIR$;" NUMBER: ";NUM$
270 IF DIR$ = "R" THEN P = P + VAL(NUM$)
280 IF DIR$ = "L" THEN P = P - VAL(NUM$)
290 IF P < 0 THEN P = P + 100
300 IF P >= 100 THEN P = P - 100
305 IF P >= 100 OR P < 0 THEN GOTO 290
310 IF P = 0 THEN Z = Z + 1
320 IF DB = -1 THEN PRINT "NEW POSITION: ";P
330 IF P = 0 THEN PRINT "ZERO COUNT: ";Z
390 GOTO 220

2000 PRINT D$;"CLOSE ";FILE$
2010 POKE 216,0
2020 PRINT "FINAL ZERO COUNT: ";Z
2030 END