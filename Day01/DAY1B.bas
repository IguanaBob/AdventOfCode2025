10 PRINT "ADVENT OF CODE 2025 - DAY 1, PART B"
20 PRINT "COPYRIGHT 2025 BY JONATHAN HULL"

100 FILE$ = "SAMPLE"
110 D$ = CHR$(4)
120 P = 50
130 Z = 0
180 DB = 1
190 ONERR GOTO 1000

195 PRINT "Reading from file: ";FILE$
200 PRINT D$;"OPEN ";FILE$
210 PRINT D$;"READ ";FILE$

220 INPUT A$
230 IF DB = 1 THEN PRINT "LINE: ";A$
240 DIR$ = LEFT$(A$,1)
250 NUM$ = MID$(A$,2)
260 IF DB = 1 THEN PRINT "DIRECTION: ";DIR$;" NUMBER: ";NUM$

280 IF DIR$ = "R" AND P + VAL(NUM$) >= 100 THEN GOSUB 2000
290 IF DIR$ = "R" THEN P = P + VAL(NUM$)

300 IF DIR$ = "L" AND P - VAL(NUM$) = 0 THEN Z = Z + 1
310 IF DIR$ = "L" AND P - VAL(NUM$) < 0 THEN GOSUB 2100
320 IF DIR$ = "L" THEN P = P - VAL(NUM$)

400 IF DB = 1 THEN PRINT "NEW POSITION: ";P
410 IF DB = 1 THEN PRINT "ZERO COUNT: ";Z
420 GOTO 220

1000 PRINT D$;"CLOSE ";FILE$
1010 POKE 216,0
1020 PRINT "FINAL ZERO COUNT: ";Z
1030 END

2000 PRINT "Subroutine: Over 99"
2010 Z = Z + 1
2020 P = P - 100
2030 IF P + VAL(NUM$) >= 100 THEN GOTO 2000
2040 RETURN

2100 PRINT "Subroutine: Under 0"
2105 IF P = 0 THEN GOTO 2120
2110 Z = Z + 1
2120 P = P + 100
2125 IF P - VAL(NUM$) = 0 THEN Z = Z + 1
2130 IF P - VAL(NUM$) < 0 THEN GOTO 2100
2140 RETURN